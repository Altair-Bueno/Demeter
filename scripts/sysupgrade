#!/usr/bin/env zsh

###############################################################################
# sysupgrade 1.0                                                              #
# Altair Bueno MIT license                                                    #
# https://github.com/Altair-Bueno/Demeter                                     #
#                                                                             #
# Updates toolchains and software installed on a macOS system                 #
#                                                                             #
# Usage:                                                                      #
# sysupgrade [flags]                                                          #
# --all       Update all                                                      #
# --homebrew  Homebrew                                                        #
# --pip       pip                                                             #
# --rust      Rust                                                            #
# --omz       Omz plugins                                                 #
# --xcode     xcode CLI tools                                                 #
###############################################################################

declare -A cli

function update_homebrew {
    brew update
    brew upgrade
    brew upgrade --cask
    brew autoremove
    brew cleanup
}

cli[--homebrew]=update_homebrew

function update_omz {
    # We spawn new zsh instances to run omz-specific commands inside
    zsh -i -c '
    for plugin in $ZSH_CUSTOM/*/*
    do
        if [[ -d "$plugin" ]]
        then
            git -C "$plugin" pull
        fi
    done
    '
}

cli[--omz]=update_omz

function update_rust {
    rustup update stable
}

cli[--rust]=update_rust

function update_xcode {
    softwareupdate --install -a
}

cli[--xcode]=update_xcode

function update_pip {
    pip install --upgrade pip
}

cli[--pip]=update_pip

if (( $*[(Ie)--all] )); then
    for v in ${(v)cli}; do
        echo "# Running $v"
        $v 
    done
else
    for arg in $*; do
        echo "# Running $cli[$arg]"
        $cli[$arg]
    done
fi

