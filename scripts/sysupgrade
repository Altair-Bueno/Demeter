#!/usr/bin/env zsh

###############################################################################
# sysupgrade 1.0                                                              #
# Altair Bueno MIT license                                                    #
# https://github.com/Altair-Bueno/Demeter                                     #
#                                                                             #
# Updates toolchains and software installed on a macOS system                 #
#                                                                             #
# Usage:                                                                      #
# sysupgrade [toolchain]                                                      #
#                                                                             #
# Toolchain Description                                                      #
# brew      Homebrew                                                         #
# pip       pip                                                              #
# rust      Rust                                                             #
# zsh       Zsh plugins                                                      #
# xcode     xcode CLI tools                                                  #
###############################################################################

declare -A cli

function update_homebrew {
    export HOMEBREW_COLOR=true
    brew update
    brew upgrade
    brew upgrade --cask
    brew autoremove
    brew cleanup
    unset HOMEBREW_COLOR
}

cli[brew]=update_homebrew

function update_zsh {
    # We spawn new zsh instances to run omz-specific commands inside
    sheldon --color=always lock --update
}

cli[zsh]=update_zsh

function update_rust {
    rustup self update
    rustup update stable
}

cli[rust]=update_rust

function update_xcode {
    softwareupdate --install -a
}

cli[xcode]=update_xcode

function update_pip {
    pip install --upgrade pip
}

cli[pip]=update_pip

function upgrade_node {
    fnm install --latest
    pnpm up -g
}

cli[node]=upgrade_node


################################################################################

if [[ -z $* ]]
then
    upgrade_list=( ${(k)cli} )
else
    upgrade_list=( $* )
fi

for upgrade in $upgrade_list
do
    ($cli[$upgrade] |& sed "s/^/[$upgrade]\t| /") &
done

wait

