#!/usr/bin/env zsh

###############################################################################
# kforward 1.0                                                                #
# Altair Bueno MIT license                                                    #
# https://github.com/Altair-Bueno/Demeter                                     #
#                                                                             #
# Port forwarding between localhost and Kubernetes services.                  #
# Lists all services from all namespaces and allows the user to choose the    #
# desired service to connect                                                  #
###############################################################################

set -e

# Args
KUBE_FORWARD_PORT_RANGE="${1:-3000-32000}"
KUBE_FORWARD_SEPARATOR="${2:-/}"

if [[ "$KUBE_FORWARD_PORT_RANGE" =~ '[[:digit:]]+-[[:digit:]]+' ]] 
then
    KUBE_FORWARD_PORT="$(shuf -i "$KUBE_FORWARD_PORT_RANGE" -n 1)"
elif [[ "$KUBE_FORWARD_PORT_RANGE" =~ '[[:digit:]]+' ]]
then
    KUBE_FORWARD_PORT="$KUBE_FORWARD_PORT_RANGE"
else
    echo "Invalid port range $KUBE_FORWARD_PORT_RANGE"
    exit 1
fi

################################################################################
# Prompt for service
KUBE_SERVICE="$(
    kubectl get svc \
        --all-namespaces \
        "-ojsonpath={range .items[*]}{.metadata.namespace}$KUBE_FORWARD_SEPARATOR{.metadata.name}{'\n'}{end}" \
        | fzf 
)"
# Extract service namespace and name
KUBE_SERVICE_NAMESPACE=$(cut "-d$KUBE_FORWARD_SEPARATOR" -f1 <<< "$KUBE_SERVICE")
KUBE_SERVICE_NAME=$(cut "-d$KUBE_FORWARD_SEPARATOR" -f2 <<< "$KUBE_SERVICE")

# Obtain service port
KUBE_SERVICE_PORT="$(
    kubectl get \
    "--namespace=$KUBE_SERVICE_NAMESPACE" \
    "service/$KUBE_SERVICE_NAME" \
    "-ojsonpath={range .spec.ports[*]}{.port}{'\n'}{end}" \
    | fzf --select-1
)"

echo "Starting port forwarding..."
echo "http://localhost:$KUBE_FORWARD_PORT"
echo "https://localhost:$KUBE_FORWARD_PORT"

kubectl port-forward \
    "--namespace=$KUBE_SERVICE_NAMESPACE" \
    "service/$KUBE_SERVICE_NAME" \
    "$KUBE_FORWARD_PORT:$KUBE_SERVICE_PORT"
